'to'⎕CY'dfns'
n ← ⊃,/{⌽⍣('y'=⊃⍵)¨(⊃n),¨⊃to/1↓n←⍎¨∊∘⎕D⍛⊆⍵}¨⊃⎕NGET'in/17.txt'1
min ← ⊃⌊/n
max ← ⊃⌈/n
m ← 1@n⊢0⍴⍨1+⊃⌈/n   ⍝ map uses 0123 for .#|~
down ← { 0≠m[⍺;⍵]:0 ⋄ m[⍺;⍵]←2 ⋄ fill/⍺⍵ }
side ← { (×m[⍺+⍺⍺;⍵])∨~2|m[⍺;⍵+1]:0 ⋄ m[⍺+⍺⍺;⍵]←2 ⋄ fill/⍺⍵+⍺⍺0 }
fill ← {
    ⍵=1⊃max:0
    _←⍺down⍵+1
    _←¯1side/⍺⍵
    _← 1side/⍺⍵
    x ← ((∪⊢,⍨⊃-1<m⌷⍨⍵,⍨⊃)⍣≡∪(∪⊢,⍨⊃+1<m⌷⍨⍵,⍨⊃)⍣≡)⍺
    0∨.=m[x;⍵]: 0
    x/⍨← 1<x⍵⌷m
    0⊣m[x;⍵] ← 2+2≠m[x;⍵+1]
}
{}fill/500 0
m ← ∊m↓⍨min×0 1
⎕← +/m∊3 2
⎕← +/m∊3
⍝← '.#|~+'[⍉m↓⍨⊃⌊/⍸m] ⍝ show map
